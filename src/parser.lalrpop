use ir::*;

grammar;

pub Input: Input = {
    Comment* <blocks:Block*> => Input { <> }
};

Comment: () = {
    r"//.*"
};

Region: String = {
    Id
};

Block: Block = {
    "block" <name:Id> "{" <statements:Statement*> Comment* <goto:Goto>
       "}" => Block { <> },
};

Goto: Vec<String> = {
    "goto" "{" <Id*> "}",
    () => Vec::new(),
};

Statement: Statement = {
    Comment* "statement" "{" <effects:Effect*> "}" => Statement { <> },
};

Effect: Effect = {
    "borrow" "(" <borrow:Id> "as" <region:Id> ")" => Effect::Borrow { <> },
    "liveOnEntry" "(" <region:Id> ")" => Effect::LiveOnEntry { <> },
    "kill" "(" <borrow:Id> ")" => Effect::Kill { <> },
    "outlives" "(" <a:Id> ":" <b:Id> ")" => Effect::Outlives { <> },
};

Id: String = {
    r"[a-zA-Z_][a-zA-Z_0-9]*" => <>.to_string(),
};
